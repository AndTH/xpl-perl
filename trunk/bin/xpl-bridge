#!/usr/bin/perl -w

=head1 NAME

xpl-bridge - Perl script for a simple xPL bridge application

=head1 SYNOPSIS

  xpl-bridge [flags] [options]
  where valid flags are:
    -h - show this help text
    -H - dump the head of all xPL messages
    -B - dump the body of all xPL messages
  and valid options are (default shown in brackets):
    -i ip  - the IP address the bridge should listen on (127.0.0.1)
    -b ip  - the broadcast address for bridge hbeat messages (127.255.255.255)
    -l ip  - the bridge should go in to listen mode listening on this
             local address
    -r ip  - the bridge should go in to client mode connecting to this
             remote address

  # start the bridge in listen mode waiting for clients on 10.0.0.1
  xpl-bridge -i eth0 -v -l 10.0.0.1

  # start a client bridge connecting to the remote bridge on 10.0.0.1
  xpl-bridge -i eth0 -v -r 10.0.0.1

=head1 DESCRIPTION

This script is an xPL bridge.  It has two modes.  If the '-l' option
is given it acts as a server ("listen mode").  If the '-r' option is
given, it acts as a client ("client/remote mode").  Listen mode causes
the bridge to listen on the specified address waiting for client mode
bridges to connect.  Once a client connects the bridge forwards local
messages to the client and the client forwards its local messages
back.

Currently, the bridge in listen mode does not redistribute messages it
receives from one remote client to another however this functionality
will be added shortly.

=cut

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
use xPL::Bridge;
$|=1; # autoflush helps debugging

my %args = ( vendor_id => 'bnz', device_id => 'bridge', );
my %opt = ();
my $verbose;
my $interface;
my $help;
my $man;
my $bridge_port;
my $local_ip;
my $remote_ip;
GetOptions('verbose+' => \$verbose,
           'interface=s' => \$interface,
           'define=s' => \%opt,
           'help|?|h' => \$help,
           'man' => \$man,
           'local_ip=s' => \$local_ip,
           'remote_ip=s' => \$remote_ip,
           'bridge_port|p=i' => \$bridge_port,
          ) or pod2usage(2);
pod2usage(1) if ($help);
pod2usage(-exitstatus => 0, -verbose => 2) if ($man);

$args{'interface'} = $interface if ($interface);
$args{'verbose'} = $verbose if ($verbose);
$args{'bridge_port'} = $bridge_port if ($bridge_port);

unless (defined $local_ip or defined $remote_ip) {
  pod2usage(-exitstatus => 1,
            -message => 'One of -local_ip or -remote_ip is required.');
}
$args{'local_ip'} = $local_ip if ($local_ip);
$args{'remote_ip'} = $remote_ip if ($remote_ip);

# Create an xPL Client object
my $xpl = xPL::Bridge->new(%args, %opt) or die "Failed to create xPL::Bridge\n";

# Run the main loop
$xpl->main_loop();

=head1 SEE ALSO

xPL::Bridge(3), xPL::Client(3), xPL::Listener(3)

Project website: http://www.xpl-perl.org.uk/

Notes about xPL bridges:
http://wiki.xplproject.org.uk/index.php/Notes_On_Bridge_Implementation

=head1 AUTHOR

Mark Hindess, E<lt>soft-xpl-perl@temporalanomaly.comE<gt>

=head1 COPYRIGHT

Copyright (C) 2005, 2008 by Mark Hindess

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.7 or,
at your option, any later version of Perl 5 you may have available.

=cut

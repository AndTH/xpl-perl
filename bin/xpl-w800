#!/usr/bin/perl -w

=head1 NAME

xpl-w800 - Perl script for an xPL W800 RF receiver application

=head1 SYNOPSIS

  xpl-w800 [flags] [options] <device>
  where valid flags are:
    -h - show this help text
    -v - verbose mode
  and valid options are (default shown in brackets):
    -i if0  - the interface for xPL messages (first non-loopback or loopback)
    -B baud - the baud rate (4800)

  # start the w800 application on first ethernet interface in verbose mode
  xpl-w800 -i eth0 -v

=head1 DESCRIPTION

This script is an xPL client that interfaces with a W800 RF receiver.

=cut

use strict;
use warnings;
use xPL::SerialClient;
use xPL::RF;
$|=1; # autoflush helps debugging

my $xpl = xPL::SerialClient->new(reader_callback => \&device_reader,
                                 baud => 4800) or
  die "Failed to create xPL::SerialClient\n";

my $rf = xPL::RF->new(source => $xpl->id) or
  die "Failed to create RF decode object\n";

# Run the main loop
$xpl->main_loop();

=head2 C<device_reader()>

This is the callback that processes output from the W800.  It is
responsible for sending out the sensor.basic xpl-trig messages.

=cut

sub device_reader {
  my ($xpl, $buf, $last) = @_;
  while (length($buf) >= 4) {
    my $m = substr($buf, 0, 4, '');
    print "Processing: ", unpack("H*", $m), "\n" if ($xpl->verbose);
    my $res = $rf->process_32bit($m);
    return 1 unless ($res && @$res);
    foreach my $msg (@$res) {
      print $msg->summary,"\n";
      $xpl->send($msg);
    }
  }
  return '';
}

# send a 'hbeat.end' message on exit
END { defined $xpl && $xpl->send_hbeat_end(); }

=head1 SEE ALSO

xPL::Client(3), xPL::Listener(3)

Project website: http://www.xpl-perl.org.uk/

W800 website: http://www.wgldesigns.com/w800.html

=head1 AUTHOR

Mark Hindess, E<lt>soft-xpl-perl@temporalanomaly.comE<gt>

=head1 COPYRIGHT

Copyright (C) 2006, 2008 by Mark Hindess

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.7 or,
at your option, any later version of Perl 5 you may have available.

=cut

#!/usr/bin/perl -w

=head1 NAME

xpl-udin - Perl script for an xPL Audon UDIN USB Relay client

=head1 SYNOPSIS

  xpl-udin [flags] [options] <device>
  where valid flags are:
    -h - show this help text
    -v - verbose mode
  and valid options are (default shown in brackets):
    -i if0  - the interface for xPL messages (first non-loopback or loopback)
    -B baud - the baud rate (9600)

  # start the udin application on first ethernet interface in verbose mode
  xpl-udin -i eth0 -v /dev/ttyUSB6

=head1 DESCRIPTION

This script is an xPL client that interfaces with a UDIN USB Relay
device.  It supports the use of control.basic messages with current
fields set to 'high', 'low', or 'pulse' with devices of the form 'oNN'
where NN is a number from 1 to 16.  There is no support for UDIN
inputs.

This script may also work with other Audon devices which have similar
firmware.  For this reason, control.basic current=pulse messages are
handled in the manner expected by an SPO-RL8 Serial Relay device even
though these will be ignored by a UDIN USB device.

=cut

use strict;
use warnings;
use English qw/-no_match_vars/;
use FileHandle;
use Getopt::Long;
use IO::Socket::INET;
use Pod::Usage;
use xPL::Client;
use xPL::Queue;
$|=1; # autoflush helps debugging

my %args = ( vendor_id => 'bnz', device_id => 'udin', );
my %opt = ();
my $verbose;
my $interface;
my $help;
my $man;
my $baud = 9600;
GetOptions('verbose+' => \$verbose,
           'interface=s' => \$interface,
           'baud=i' => \$baud,
           'define=s' => \%opt,
           'help|?|h' => \$help,
           'man' => \$man,
          ) or pod2usage(2);
pod2usage(1) if ($help);
pod2usage(-exitstatus => 0, -verbose => 2) if ($man);
$args{'interface'} = $interface if ($interface);
$args{'verbose'} = $verbose if ($verbose);

my $dev = shift or pod2usage(-message => "The device parameter is required",
                             -exitstatus => 1);

# Create an xPL Client object
my $xpl = xPL::Client->new(%args, %opt) or die "Failed to create xPL::Client\n";

my $queue = xPL::Queue->new;
my $waiting = undef;

# Add a callback to receive all incoming xPL messages
$xpl->add_xpl_callback(id => 'udin', callback => \&xpl_in,
                       filter => {
                                  message_type => 'xpl-cmnd',
                                  class => 'control',
                                  class_type => 'basic',
                                  type => 'output',
                                 });

my $buf = q{};

my $fh;
if ($dev =~ /\//) {
  # TODO: use Device::SerialPort?
  system("/bin/stty -F $dev ospeed $baud pass8 raw >/dev/null") == 0 or
    die "Setting serial port with stty failed: $!\n";
  $fh = FileHandle->new;
  sysopen($fh, $dev,O_RDWR|O_NOCTTY|O_NDELAY)
    or die "Cannot open serial connection on device '$dev'\n";
  $fh->autoflush(1);
  binmode($fh);
} else {
  $dev .= ':10001' unless ($dev =~ /:/);
  $fh = IO::Socket::INET->new($dev)
    or die "Cannot TCP connection to device at '$dev'\n";
}
$xpl->add_input(handle => $fh, callback => \&udin_reader);

queue_udin('?');

# Run the main loop
$xpl->main_loop();

=head2 C<xpl_in(%xpl_callback_parameters)>

This is the callback that processes incoming xPL messages.  It handles
the incoming control.basic schema messages.

=cut

sub xpl_in {
  my %p = @_;
  my $msg = $p{message};
  my $peeraddr = $p{peeraddr};
  my $peerport = $p{peerport};

  if ($msg->device eq 'debug') {
    queue_udin('?');
    queue_udin('s0');
  }
  return 1 unless ($msg->device =~ /^o(\d+)$/);
  my $num = $LAST_PAREN_MATCH;
  my $command = lc $msg->current;
  if ($command eq "high") {
    queue_udin(sprintf("n%d", $num));
  } elsif ($command eq "low") {
    queue_udin(sprintf("f%d", $num));
  } elsif ($command eq "pulse") {
    queue_udin(sprintf("n%d", $num));
    #select(undef,undef,undef,0.15);
    queue_udin(sprintf("f%d", $num));
  } elsif ($command eq "toggle") {
    queue_udin(sprintf("t%d", $num));
  }
  return 1;
}

=head2 C<udin_reader()>

This is the callback that processes output from the UDIN.  It is
responsible for sending out the sensor.basic xpl-trig messages.

=cut

sub udin_reader {
  my $bytes = $fh->sysread($buf, 512, length($buf));
  while ($buf =~ s/^(.*?)\r\n//) {
    my $line = $LAST_PAREN_MATCH;
    next unless (defined $line && $line ne '');
    my $time = time;
    if ($waiting && lc $line eq $waiting) {
      print "received command response: $line\n" if ($verbose);
    } else {
      print "received: '$line'\n" if ($verbose);
    }
    undef $waiting;
    send_udin();
  }
  return 1;
}

=head2 C<queue_udin( $message )>

This functions is used to queue text commands to be sent to the UDIN.

=cut

sub queue_udin {
  my $msg = shift;
  $queue->enqueue($msg);
  print "queued: $msg\n" if ($verbose);
  if (!defined $waiting) {
    return send_udin();
  }
  return 1;
}

=head2 C<send_udin( $message )>

This functions is used to send text commands to the UDIN.

=cut

sub send_udin {
  my $msg = $queue->dequeue;
  return if (!defined $msg);
  print "sending: $msg\n" if ($verbose);
  $waiting = $msg;
  $msg .= "\r";
  syswrite($fh, $msg, length($msg));
  $fh->flush();
}

# send a 'hbeat.end' message on exit
END { defined $xpl && $xpl->send_hbeat_end(); }

=head1 SEE ALSO

xPL::Client(3), xPL::Listener(3)

Project website: http://www.xpl-perl.org.uk/

UDIN website: http://www.audon.co.uk/udin.html

=head1 AUTHOR

Mark Hindess, E<lt>soft-xpl-perl@temporalanomaly.comE<gt>

=head1 COPYRIGHT

Copyright (C) 2008 by Mark Hindess

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.7 or,
at your option, any later version of Perl 5 you may have available.

=cut

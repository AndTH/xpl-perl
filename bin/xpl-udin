#!/usr/bin/perl -w

=head1 NAME

xpl-udin - Perl script for an xPL Audon UDIN USB Relay client

=head1 SYNOPSIS

  xpl-udin [flags] [options] <device>
  where valid flags are:
    -h - show this help text
    -v - verbose mode
  and valid options are (default shown in brackets):
    -i if0  - the interface for xPL messages (first non-loopback or loopback)
    -B baud - the baud rate (9600)

  # start the udin application on first ethernet interface in verbose mode
  xpl-udin -i eth0 -v /dev/ttyUSB6

=head1 DESCRIPTION

This script is an xPL client that interfaces with a UDIN USB Relay
device.  It supports the use of control.basic messages with current
fields set to 'high', 'low', or 'pulse' with devices of the form 'oNN'
where NN is a number from 1 to 16.  There is no support for UDIN
inputs.

This script may also work with other Audon devices which have similar
firmware.  For this reason, control.basic current=pulse messages are
handled in the manner expected by an SPO-RL8 Serial Relay device even
though these will be ignored by a UDIN USB device.

=cut

use strict;
use warnings;
use English qw/-no_match_vars/;
use xPL::SerialClientLine;
$|=1; # autoflush helps debugging

sub process_line;
my $xpl = xPL::SerialClient->new(reader_callback => \&process_line);

# Add a callback to receive all incoming xPL messages
$xpl->add_xpl_callback(id => 'udin', callback => \&xpl_in,
                       filter => {
                                  message_type => 'xpl-cmnd',
                                  class => 'control',
                                  class_type => 'basic',
                                  type => 'output',
                                 });

$xpl->write('?');

# Run the main loop
$xpl->main_loop();

=head2 C<xpl_in(%xpl_callback_parameters)>

This is the callback that processes incoming xPL messages.  It handles
the incoming control.basic schema messages.

=cut

sub xpl_in {
  my %p = @_;
  my $msg = $p{message};
  my $peeraddr = $p{peeraddr};
  my $peerport = $p{peerport};

  if ($msg->device eq 'debug') {
    $xpl->write('?');
    $xpl->write('s0');
  }
  return 1 unless ($msg->device =~ /^o(\d+)$/);
  my $num = $LAST_PAREN_MATCH;
  my $command = lc $msg->current;
  if ($command eq "high") {
    $xpl->write(sprintf("n%d", $num));
  } elsif ($command eq "low") {
    $xpl->write(sprintf("f%d", $num));
  } elsif ($command eq "pulse") {
    $xpl->write(sprintf("n%d", $num));
    #select(undef,undef,undef,0.15);
    $xpl->write(sprintf("f%d", $num));
  } elsif ($command eq "toggle") {
    $xpl->write(sprintf("t%d", $num));
  }
  return 1;
}

=head2 C<process_line()>

This is the callback that processes lines of output from the UDIN.  It
is responsible for sending out the sensor.basic xpl-trig messages.

=cut

sub process_line {
  my ($xpl, $line) = shift;
  return unless (defined $line && $line ne '');
  print "received: '$line'\n" if ($xpl->verbose);
  return 1;
}

# send a 'hbeat.end' message on exit
END { defined $xpl && $xpl->send_hbeat_end(); }

=head1 SEE ALSO

xPL::Client(3), xPL::Listener(3)

Project website: http://www.xpl-perl.org.uk/

UDIN website: http://www.audon.co.uk/udin.html

=head1 AUTHOR

Mark Hindess, E<lt>soft-xpl-perl@temporalanomaly.comE<gt>

=head1 COPYRIGHT

Copyright (C) 2008 by Mark Hindess

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.7 or,
at your option, any later version of Perl 5 you may have available.

=cut

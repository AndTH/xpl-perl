#!/usr/bin/perl -w

# $Id$

=head1 NAME

xpl-logger - Perl script for an example xPL logger application

=head1 SYNOPSIS

  # show usage message
  xpl-logger -h

  # start the logger listening and broadcasting on the loopback interface
  xpl-logger -i 127.0.0.1 -b 127.255.255.255

=head1 DESCRIPTION

This script is an xPL client that logs all xPL incoming messages.

=cut

use strict;
use warnings;
use Getopt::Std;
use xPL::Client;
$|=1; # autoflush helps debugging

sub HELP_MESSAGE {
  my $msg = shift || "";
  die
qq{Usage: $0 [flags] [options]
where valid flags are:
  -h - show this help text
  -H - dump the head of all xPL messages
  -B - dump the body of all xPL messages
  -v - verbose client mode
and valid options are (default shown in brackets):
  -i if0 - the inferface for xPL messages (first non-loopback or loopback)
$msg
};
}

my %opt = ();
getopts('hvi:BH', \%opt);
HELP_MESSAGE() if (exists $opt{'h'});
my %args =
  (
   vendor_id => "bnz",
   device_id => "listener",
  );
if (exists $opt{'i'}) {
  $args{interface} = $opt{'i'};
}
if (exists $opt{'v'}) {
  $args{verbose} = $opt{'v'};
}

my $verbose_head = exists $opt{'H'};
my $verbose_body = exists $opt{'B'};

# Create an xPL Client object
my $xpl = xPL::Client->new(%args) or die "Failed to create xPL::Client\n";

# Add a callback to receive all incoming xPL messages
$xpl->add_xpl_callback(id => "logger",
                       self_skip => 0, targetted => 0,
                       callback => \&log);

# Run the main loop
$xpl->main_loop();

# The callback to log the incoming messages
sub log {
  my %p = @_;
  my $msg = $p{message};
  my $peeraddr = $p{peeraddr};
  my $peerport = $p{peerport};

  print "$peeraddr:$peerport [", $msg->summary, "]\n";
  print $msg->head_string if ($verbose_head);
  print $msg->body_string if ($verbose_body);
  return 1;
};

# send a "hbeat.end" message on exit
END { defined $xpl && $xpl->send_hbeat_end(); }

=head1 SEE ALSO

xPL::Client(3), xPL::Listener(3)

Project website: http://www.xpl-perl.org.uk/

=head1 AUTHOR

Mark Hindess, E<lt>xpl-perl@beanz.uklinux.netE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2005 by Mark Hindess

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself, either Perl version 5.8.7 or,
at your option, any later version of Perl 5 you may have available.

=cut
